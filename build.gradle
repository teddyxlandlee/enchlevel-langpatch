buildscript {
    repositories {
        maven {
            name = 'Aliyun Mirror'
            url  = 'https://maven.aliyun.com/repository/public'
        }
        gradlePluginPortal()
    }
    dependencies {
        classpath("com.google.guava:guava:33.5.0-jre")
        classpath("org.ow2.asm:asm:9.3")
    }
}

plugins {
    id 'maven-publish'
    id 'java'
    id 'idea'
    id "me.modmuss50.mod-publish-plugin" version "0.5.1"
    id 'xland.gradle.forge-init-injector' version '1.1.1'
}

base {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    archivesName = project.archives_base_name
}

group = project.maven_group
version = project.mod_version

allprojects {
    repositories {
        maven {
            name = 'Aliyun Mirror'
            url  = 'https://maven.aliyun.com/repository/public'
        }
        maven {
            name = 'Fabric'
            url = 'https://maven.fabricmc.net/'
        }
        maven {
            name = 'Lss233 Mirror'
            url  = 'https://lss233.littleservice.cn/repositories/minecraft'
        }
        maven {
            name = '7c7maven'
            // Formerly COVID-Trump
            url = 'https://mvn.7c7.icu'
        }
    }
}

dependencies {
    implementation "net.fabricmc:fabric-loader:${project.loader_version}"
    implementation 'com.google.guava:guava:21.0'
    implementation 'it.unimi.dsi:fastutil:8.2.1'
    implementation 'net.fabricmc:sponge-mixin:0.11.4+mixin.0.8.5'
    implementation 'org.apache.logging.log4j:log4j-api:2.8.1'

    compileOnly 'org.apiguardian:apiguardian-api:1.1.2'
    compileOnly 'org.jetbrains:annotations:26.0.2'
    testRuntimeOnly 'org.apache.logging.log4j:log4j-core:2.20.0'
}

forgeInitInjector {
    modId = 'enchlevellangpatch'
    stubPackage = 'g4QNAmBS\$BFhdWlbozC0\$'
    setClientEntrypoint 'xland/mcmod/enchlevellangpatch/impl/LangPatchImpl'
    neoFlag 'pre_20_5', 'post_20_5'
}

processResources {
    inputs.property "version", project.version

    filesMatching(['fabric.mod.json', 'META-INF/mods.toml', 'META-INF/neoforge.mods.toml']) {
        expand 'version' : project.version
    }
}

import com.google.common.hash.Hashing
import com.google.common.hash.HashCode

tasks.register("checkValueTableSum") {
    def valueTableFile = file("src/main/resources/xland/mcmod/enchlevellangpatch/impl/ValueTable.txt")
    def expectedHash = '3c09cc78904fc47fd583b680ecfa9e2ad7370787ea149d843a56fb8f8c15c8d4'

    doLast {
        def hasher = Hashing.sha256().newHasher()
        valueTableFile.withInputStream {input ->
            input.eachByte { b -> hasher.putByte(b) }
        }
        assert hasher.hash() == HashCode.fromString(expectedHash)
    }
}

processResources.finalizedBy 'checkValueTableSum'

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile.class).configureEach {
    it.options.encoding = "UTF-8"

    def targetVersion = 8
    if (JavaVersion.current().isJava9Compatible())
        it.options.release.set targetVersion
}

java {
    withSourcesJar()
    //withJavadocJar()
}

javadoc {
    failOnError false
    options {
        locale 'en_US'
        encoding 'UTF-8'
        charSet 'UTF-8'
        links(
                'https://docs.oracle.com/javase/8/docs/api/',
                'https://javadoc.io/doc/com.google.guava/guava/21.0/',
                'https://javadoc.io/doc/org.jetbrains/annotations/19.0.0/',
                'https://javadoc.io/doc/org.apiguardian/apiguardian-api/1.1.2/',
        )
        include 'xland/mcmod/enchlevellangpatch/api/**'
    }
}

import java.time.Instant
import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter

jar {
    from ("LICENSE") {
        rename {  "META-INF/${it}_${project.archives_base_name}"  }
    }
    manifest {
        attributes([
                "Specification-Title"     : "Enchantment Level Language Patch",
                "Specification-Vendor"    : "teddyxlandlee",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.version,
                "Implementation-Vendor"   : "teddyxlandlee",
                "Implementation-Timestamp": Instant.now(),
                "MixinConfigs" : "ellp-forge.mixins.json",  // for Forge FML
        ])
    }
}

import javax.inject.Inject

abstract class PackTemplate extends Jar {
    @Input
    final String templateId

    private static final def DATE_FORMATTER = DateTimeFormatter.ofPattern('yyyyMMdd')
    private static final def PKG_ENCODER = Base64.urlEncoder.withoutPadding()

    private static final String b64encode(String input) {
        return PKG_ENCODER.encodeToString(input.getBytes('utf-8')).replace('-', '$')
    }

    @Inject
    PackTemplate(String templateId) {
        this.templateId = templateId
        def stubModId = 'lpstub_' + templateId

        configure {
            def packVersion = "${DATE_FORMATTER.format(ZonedDateTime.now())}.${project.template_patch_number ?: '0'}"

            def templateProperties = [
                    'pack_id': stubModId,
                    'pack_version': packVersion,
                    'pack_desc': "Generated stub pack for enchlevel-langpatch: '${templateId}'",
            ]

            inputs.properties templateProperties

            from(project.file('pack_templates/fmj_templates.json')) {
                rename { 'fabric.mod.json' }
                expand(templateProperties)
            }
            ['META-INF/mods.toml', 'META-INF/neoforge.mods.toml'].each {fn ->
                from(project.file('pack_templates/modinfo_templates.toml')) {
                    rename { fn }
                    expand(templateProperties)
                }
            }
            ['en_us', 'zh_cn', 'lzh'].each {langCode ->
                def targetFile = "assets/langpatch-generated-stubpack/lang/${langCode}.json"
                from(project.file("pack_templates/langs/${templateId}.json")) {
                    rename { targetFile }
                }
            }
            from(project.file('pack_templates/pack.mcmeta')) {
                rename { 'pack.mcmeta' }
            }

            this.archiveFileName.convention(project.provider {
                "lpstub-${templateId}-${packVersion}.jar"
            })

            def pkgName = "lpstub_${PackTemplate.b64encode(templateId)}"
            def classFile = project.layout.buildDirectory.dir('tmp/lpstub').map { it.file(pkgName + '.bin') }
            def className = pkgName + '/a'
            def genClass = project.tasks.register("packTemplateModClass_${templateId}") {
                def parts = [
                        "yv66vgAAADQAEQE=", // ClassFile Header
                        // CP#1: utf8 modId
                        "AQ==", // Constant '1'
                        // CP#2: utf8 className
                        "BwACAQAQamF2YS9sYW5nL09iamVjdAcABAEAI0xuZXQvbWluZWNyYWZ0Zm9yZ2UvZm1sL2NvbW1" +
                                "vbi9Nb2Q7AQAFdmFsdWUBAB5MbmV0L25lb2ZvcmdlZC9mbWwvY29tbW9uL01vZDsBAA" +
                                "Y8aW5pdD4BAAMoKVYMAAkACgoABQALAQAJR2VuZXJhdGVkAQAEQ29kZQEAClNvdXJjZ" +
                                "UZpbGUBABlSdW50aW1lVmlzaWJsZUFubm90YXRpb25zACEAAwAFAAAAAAABAAEACQAK" +
                                "AAEADgAAABEAAQABAAAABSq3AAyxAAAAAAACAA8AAAACAA0AEAAAABQAAgAGAAEAB3M" +
                                "AAQAIAAEAB3MAAQ==",    // Rest of classfile
                ]
                doLast {
                    def f = classFile.get().asFile
                    f.parentFile.mkdirs()
                    f.withDataOutputStream {dos ->
                        dos.write(parts[0].decodeBase64())
                        dos.writeUTF(stubModId) // modId
                        dos.write(parts[1].decodeBase64())
                        dos.writeUTF(className) // className
                        dos.write(parts[2].decodeBase64())
                    }
                }
            }

            from(genClass) {
                rename { className + '.class' }
            }

            destinationDirectory.set project.layout.buildDirectory.dir('lpstub')
        }
    }
}

tasks.register('packAllArabic', PackTemplate.class, 'all_arabic')
tasks.register('packAllRoman', PackTemplate.class, 'roman')

tasks.register('templatePacks') {
    dependsOn 'packAllArabic', 'packAllRoman'
}

tasks.build {
    dependsOn 'templatePacks'
}

import me.modmuss50.mpp.ReleaseType

static def javaVersions(int from, int to) {
	List<JavaVersion> list = []
	for (int i = from; i <= to; i++) {
		list.add(JavaVersion.toVersion(i))
	}
	return list
}

publishMods {
	file = jar.archiveFile
	["fabric", "forge", "neoforge", "quilt"].each { ml ->
		modLoaders.add(ml)
	}
	type = ReleaseType.of(project.release_type)
	changelog = project.changelog
	displayName = "[1.16+/ALL] LangPatch ${project.version}"

	curseforge {
		projectId = "529854"
		minecraftVersionRange {
			start = "1.16"
			end = "latest"
		}
		javaVersions(8, 22).each { jv ->
			javaVersions.add(jv)
		}
		clientRequired = true
		serverRequired = false
		accessToken = providers.environmentVariable("CURSEFORGE_API_KEY")
	}

	modrinth {
		projectId = "Lf4kDKU9"
		minecraftVersionRange {
			start = "1.16"
			end = "latest"
			includeSnapshots = true
		}
		accessToken = providers.environmentVariable("MODRINTH_TOKEN")
	}

    additionalFiles.from(
            tasks.sourcesJar
    )

    additionalFiles.from(
            tasks.packAllArabic,
            tasks.packAllRoman,
    )
}

// configure the maven publication
publishing {
    publications {
        create('mavenJava', MavenPublication.class) {
            // add all the jars that should be included when publishing to maven
            artifact(jar) {
                builtBy jar
            }
            artifact(sourcesJar) {
                builtBy sourcesJar
            }
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        mavenLocal()
    }
}
